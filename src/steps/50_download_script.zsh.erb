# Download zimfw script
readonly ZTARGET=${ZIM_HOME}/zimfw.zsh
if (
  command mkdir -p ${ZIM_HOME} || return 1
  readonly ZURL=https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  if [[ ${+commands[curl]} -ne 0 && -x ${commands[curl]} ]]; then
    command curl -fsSL -o ${ZTARGET} ${ZURL} || return 1
  elif [[ ${+commands[wget]} -ne 0 && -x ${commands[wget]} ]]; then
    command wget -nv -O ${ZTARGET} ${ZURL} || return 1
  else
    print -u2 "${ZERROR}Either ${ZBOLD}curl${ZNORMALRED} or ${ZBOLD}wget${ZNORMALRED} are required to download Zim Framework script.${ZNORMAL}"
    return 1
  fi
); then
  print -R "${ZOKAY}Downloaded Zim Framework script to ${ZBOLD}${(Dq+)ZTARGET}${ZNORMAL}"
else
  command rm -rf ${ZIM_HOME}
  print -u2 -R "${ZERROR}Could not download Zim Framework script to ${ZBOLD}${(Dq+)ZTARGET}${ZNORMALRED}${ZNORMAL}"
  return 1
fi
